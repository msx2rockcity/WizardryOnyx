1110 '==== INITIALIZE ====
1120 KEYOFF:CLEAR 500,&HBAFF:BLOAD"program.bin"
1130 DEFINT A-Z:SCREEN 1,0,0:COLOR 15,1,1
1140 DIM MZ(16,16)'|MAPPING FLAG 1bit|POINT EVENT 3bit|MAPPING DATA 4bit｜MAP DATA 8bit|
1150 WX=0:WY=0:WW=0:WH=0 '汎用ウインドウ表示ルーチン変数
1160 FOR Y=0 TO 15:FOR X=0 TO15:MZ(X,Y)=0:NEXT X,Y:SS=0
1190 DI$(0)="NORTH":DI$(1)="EAST ":DI$(2)="SOUTH":DI$(3)="WEST "
1200 WL$(0)="NO WALL": WL$(1)="TWALL":WL$(2)="DOOR":WL$(3)="WALL"
1210 GOSUB4530:CLS:GOSUB4550:CLS:LOCATE 0,1:PRINT "PARAMETER INIT"
1220 '--MACHINE LANGUAGE ROUTINE SET--
1230 DEF USR0=&HC300: 'MAP CHARA MAKE
1240 DEF USR1=&HC000: 'MAZE DISP
1250 DEF USR2=&HC400: 'BGM ON
1260 DEF USR3=&HC445: 'BGM OFF
1270 DEF USR4=&HBF00: 'MOSAIC DELETE
1280 DEF USR5=&HBF0A: 'MOSAIC DISP
1290 DEF USR6=&HBE00  'WINDOW DISP(未完成)
1300 A=USR0(0) 'CHARA MAKING
1310 LOCATE 0,3:PRINT "CHARA MAKING COMPLETE"
1320 '--ON STOP GOSUB SET--
1330 ON STOP GOSUB 4620:STOP ON
1340 LOCATE 0,5:PRINT "ON STOP GOSUB SET"
1350 '--SPRITE DATA SET-- 
1360 GOSUB 4070:LOCATE 0,7:PRINT "SPRITE MAKING COMPLETE"
1370 '--WALL COLOR READ--
1380 CO=0:GOSUB 4140:LOCATE 0,9:PRINT "COLOR SET COMPLETE"
1390 '--AUTO MAPPING INIT AND MAP DATA INIT--
1400 VDP(2)=11:GOSUB 4530:GOSUB 3890
1410 CLS:GOSUB 4210:GOSUB 4410
1420 '======== TITLE DISP ========
1430 VDP(2)=6:GOSUB 4550:CLS:B=0:WX=4:WY=8:WW=19:WH=9:GOSUB 3110:LOCATE 7,10:PRINT "WIZARDRY ONIX":C=0
1440 COLOR (C MOD 13)+3,0,0:
1445 IF((C MOD 32)=16) THEN WX=5:WY=13:WW=17:WH=3:GOSUB 3110:LOCATE 6,14:PRINT "PUSH SPACE OR A"
1447 IF((C MOD 32)=0)  THEN LOCATE 5,13:PRINT "                 ":LOCATE 5,14:PRINT "                 ":LOCATE 5,15:PRINT "                 "
1448 LOCATE 0,23:PRINT"ver1_5_2";CHR$(&HB)
1450 S=STRIG(0) OR STRIG(1):IF(S <> 0) THEN 1460
1455 C=C+1:IF C >= 32700 THEN C=0: GOTO 1440
1457 GOTO 1440
1460 RT=0:PX=0:PY=0:TX=0:TY=0:TR=0:WK=0:SM=1:ST=0:WX=0:WY=0:WW=0:WH=0 :CO=0:GT=0:GO=0:OT=0:TM=0:TS=0:TN=0:TT=0:COLOR 15,0,0:VPOKE VP,C1(C0):VPOKE VP+1,C2(C0):VPOKE VP+2,C3(C0)
1470 '--BGM SET BGM ROUTINE--
1480 GOSUB 4480
1490 '--SET MAP DISP POINT--
1500 Q=&HC125:POKE Q,RT:POKE Q+1,PX:POKE Q+2,&HC2+PY
1510 '--MAP DISP INIT--
1520 SM=1:A=USR2(0)
1530 GOTO 2000
2000 '==== MAIN ROUTINE ====
2010 TIME = 0
2020 PC=1'STEP
2030 CT=0'COUNTER
2040 '-- MAIN LOOP --
2050 ON PC GOTO 2060,2090,2370,2390,2490,2580
2060 '-- PC = 1 MOVE INIT --
2070 GOSUB 3230:GOSUB 3140:GOSUB 4550:A=USR1(0)
2080 VDP(2)=6:PC=2:CT=0:GOTO 2050
2090 '-- PC = 2 MOVE --
2100 GOSUB 3040 'TIME CALC
2110 IF((MZ(PX,PY) AND &HFF) = &HFF) THEN PC=5:GOTO 2050'IN STONE
2120 IF(WK=(256-SS)) THEN PC=6:GOTO 2050 'GAMECLEAR
2130 S=STICK(0) OR STICK(1):PK=PEEK(&HFBE9)AND 4
2140 IF(STRIG(0)=0 AND STRIG(1)=0 AND S=0 AND S<9 AND PK<>0) THEN ST=ST+1 ELSE ST=0 'SPACE CURSOL KEY NOT INPUT TIME
2150 IF(ST=10) THEN GOSUB 3930:GOSUB 3460:GOSUB 3580 'IF INPUT OFF TIME OVER 10 THEN WINDOW DISP
2160 IF(ST>10) THEN GOSUB 3600 'TIME DISP
2170 IF(STRIG(0)<>0 OR STRIG(1)<>0) THEN ST=0:GOSUB 3260 'COLOR CHANGE
2180 W = ((MZ(PX,PY) AND &H7FFF) \ (4^RT)) AND 3'FRONT WALL
2190 IF(S=1) THEN GOTO 2200 ELSE 2220
2200 IF(W<>3) THEN PX=(PX-(RT=1)+(RT=3))AND15:PY=(PY-(RT=0)+(RT=2))AND15:GOSUB 3140:A=USR1(0):GOSUB 3820
2210 IF(W=3) THEN GOSUB 3280:GOSUB 3690:A=USR1(0):GOTO 2240'WALL MESSAGE,WALL SOUND,WALL SOUNDOFF
2220 IF((S<9)AND((S\2)<>0)) THEN RT=(RT+(S\2))AND3:GOSUB 3140:A=USR1(0):GOSUB 3820'TURN
2230 S=((PEEK(&HFBEB)AND4)=0):IF(S<>0) THEN PC=3:GOTO 2360 'MAPPING DISP
2240 IF(CT>0) THEN CT=CT-1
2250 IF(CT=0 AND PK=0 AND SM=1)THEN A=USR3(0):SM=0:CT=2'BGM OFF
2260 IF(CT=0 AND PK=0 AND SM=0)THEN A=USR2(0):SM=1:CT=2'BGM ON
2270 LOCATE 0,0:PRINT "FREE";FRE(0)
2280 'LOCATE 0,1:PRINT USING "###:##:##";TM,TS,TN
2290 'POINT EVENT
2300 PP=(MZ(PX,PY)AND&H7FFF)\(2^12):PP=PP AND 7
2310 IF(PP = 0) THEN GOTO 2360
2320 'TELEPORTER
2330 IF(PP = 1) THEN A=RND(-TIME):TX=INT(RND(1)*16!):TY=INT(RND(1)*16!):TR=INT(RND(1)*4!):GOSUB 3010:ST=0:GOTO 2360
2340 'TURN TABLE
2350 IF(PP = 2) THEN RT=(RT+1) AND 3:GOSUB3140:ST=0:A=USR1(0):FOR I=0 TO100 : NEXT I
2360 GOTO 2050 'MAIN LOOP
2370 '-- PC = 3 MAPPING DISP INIT --
2380 GOSUB 4530:GOSUB 3580:VDP(2)=11:GOSUB 3200:GOSUB 3460:PC=4:GOTO 2050
2390 '-- PC = 4 MAPPING DISP --
2400 GOSUB 3040'TIME CALC
2410 PK=PEEK(&HFBE9)AND 4
2420 GOSUB 3600
2430 IF(CT>0) THEN CT=CT-1
2440 IF(CT=0 AND PK=0 AND SM=1)THEN A=USR3(0):SM=0:CT=2'BGM OFF
2450 IF(CT=0 AND PK=0 AND SM=0)THEN A=USR2(0):SM=1:CT=2'BGM ON
2460 LOCATE 0,20:PRINT "FREE";FRE(0)
2470 S=((PEEK(&HFBEB)AND4)=0)ORSTRIG(3):IF(S<>0) THEN ST=0: PC=1
2480 GOTO 2050
2490 '-- PC = 5 IN STONE GAMEOVER --
2500 GOSUB 3340
2510 FOR I=0 TO 12000: NEXT
2520 GOSUB 3370
2530 C=0:FOR I=0 TO 250
2540 C=3-C:VP=&H2002:VPOKE VP,C1(C):VPOKE VP+1,C2(C):VPOKE VP+2,C3(C)
2550 NEXT I
2560 GOSUB 5490:GOSUB 5010:GOSUB 5670
2570 GOTO 4570
2580 '-- PC = 6 GAMECLEAR --
2590 GOSUB 3400
2600 FOR I=0 TO 300
2610 C=I MOD 5:VP=&H2002:VPOKE VP,C1(C):VPOKE VP+1,C2(C):VPOKE VP+2,C3(C)
2620 NEXT I
2630 GOSUB 5310:GOSUB 5010:GOSUB 5670
2640 GOTO 4570
3000 '==== SUB ROUTINE ====
3010 '--TELEPORTER--
3020 GOSUB 3310:FOR I=0 TO 500:NEXT I
3030 A=RND(-TIME):PX=INT(RND(1)*16!):PY=INT(RND(1)*16!):RT=INT(RND(1)*4!):GOSUB 3140:A=USR4(0):VPOKE VP,0:VPOKE VP+1,0:VPOKE VP+2,0:A=USR1(0):A=USR4(0):VPOKE VP,C1(CO):VPOKE VP+1,C2(CO):VPOKE VP+2,C3(CO):A=USR5(0):RETURN
3040 '--TIME CALC--
3050 IF(TIME > 30000) THEN OT=0:TIME=TIME-30000'TIME回り込み
3060 TT=TIME-OT:OT=TIME
3070 TN=TN+TT
3080 IF(TN>=60) THEN TS=TS+1:TN=TN-60:IF (TN>=60) THEN GOTO 3080'引きつり時に大幅にTNが60を超えた場合の対処
3090 IF(TS>=60) THEN TM=TM+1:TS=TS-60:IF (TS>=60) THEN GOTO 3090'同上
3100 RETURN
3110 '--WINDOW DISP--
3120 LOCATE WX,WY:PRINT"(";STRING$(WW-2,")");"*":FOR H=1 TO WH-2:LOCATE WX,WY+H:PRINT"+";SPC(WW-2);",":NEXT H:
3130 LOCATE WX,WY+WH-1:PRINT"-";STRING$(WW-2,".");"/":RETURN
3140 'MAPPING SET
3150 W = ((MZ(PX,PY) AND &H7FFF) \ 256) AND 15
3160 VPOKE &H2E02-PY*32+PX+1,W
3170 POKE Q,RT:POKE Q+1,PX+PY*16
3180 IF ((MZ(PX,PY) AND &H8000) = 0) THEN MZ(PX,PY)=MZ(PX,PY) OR &H8000:WK=WK+1'歩いたビットを立てる
3190 RETURN
3200 '--SPRITE SET,ON-- 
3210 PUT SPRITE 0,(24+PX*8,127-PY*8),7,RT:P=PEEK(PEEK(Q+2)*256+PY*16+PX):OP=0:FORI=0TO3:OP=OP\2-(PMOD2=1)*8:P=P\4:NEXT:VPOKE&H2E02-PY*32+PX+1,OP:
3220 RETURN
3230 '--SPRITE RESET,OFF--
3240 PUT SPRITE 0, (0,0),0,0
3250 RETURN
3260 '--COLOR CHANGE--
3270 A=USR4(0):CO=(CO+1)MOD 5:VPOKE VP,C1(CO):VPOKE VP+1,C2(CO):VPOKE VP+2,C3(CO):A=USR5(0) : RETURN
3280 '--WALL MESSAGE--
3290 WX=7:WY=11:WW=14:WH=3:GOSUB 3110
3300 LOCATE WX+1,WY+1:PRINT"かべにぶつかっている":RETURN
3310 '--WARP MESSAGE--
3320 WX=8:WY=11:WW=12:WH=3:GOSUB3110
3330 LOCATE WX+4,WY+1:PRINT"おおっと　":RETURN
3340 '--IN STONE MESSAGE--
3350 WX=9:WY=11:WW=10:WH=3:GOSUB3110
3360 LOCATE WX+1,WY+1:PRINT"いしのなかにいる":RETURN
3370 '--GAMEOVER MESSAGE--
3380 WX=6:WY=15:WW=16:WH=5:GOSUB3110
3390 LOCATE WX+3,WY+2:PRINT"GAME  OVER":RETURN
3400 '--GAMECLEAR MESSAGE
3410 WX=5:WY=8:WW=18:WH=3:GOSUB3110
3420 LOCATE WX+1,WY+1:PRINT"すべてのマップをうめました。"
3430 WX=6:WY=15:WW=16:WH=5:GOSUB3110
3440 LOCATE WX+3,WY+2:PRINT"GAME CLEAR":
3450 RETURN
3460 '--INFORMATION DISP--
3470 WX=18:WY=0:WW=10:WH=10:GOSUB3110
3480 LOCATE WX+1,WY+1:PRINT"X";PX
3490 LOCATE WX+1,WY+2:PRINT"Y";PY
3500 LOCATE WX+1,WY+3:PRINT"D ";DI$(RT)
3510 LOCATE WX+1,WY+4:PRINT "WK";WK:
3520 LOCATE WX+1,WY+5:PRINT "PER";INT(100!*(WK/(256!-SS)))
3530 'LOCATE WX+1,WY+6:PRINT USING "STONE ##";SS
3540 LOCATE WX+1,WY+7:PRINT "FRONT"
3550 W = ((MZ(PX,PY) AND &H7FFF) \ (4^RT)) AND 3
3560 LOCATE WX+2,WY+8:PRINT WL$(W);CHR$(&HB)
3570 RETURN
3580 '--TIME DISP--
3590 WX=18:WY=10:WW=10:WH=5:GOSUB 3110
3600 LOCATE 19,11:PRINT "TIME"
3610 IF(TM < 100) THEN LOCATE 19,13:PRINT USING "##:##:##";TM,TS,TN:RETURN
3620 IF(TM >=100) THEN LOCATE 19,13:PRINT "99:59:59"
3630 RETURN
3640 '--WALL SOUND ON BGM--
3650 FOR I=0 TO 17:SOUND 8,15:SOUND 9,0:SOUND 10,0:SOUND 0,100:SOUND 1,10:NEXT
3660 FOR I=0 TO  2:SOUND 8, 0:SOUND 9,0:SOUND 10,0:NEXT
3670 IF PLAY(0) THEN 3670
3680 RETURN
3690 '--WALL SOUND OFF BGM--
3700 IF(SM=1) THEN GOSUB 3640:RETURN
3710 SOUND 0,100:SOUND 1,10
3720 SOUND 7,&HBE:SOUND 8,&H10
3730 SOUND 11,100:SOUND 12,14
3740 SOUND 13,0
3750 IF PLAY(0) THEN 3750
3760 RETURN
3770 '--MOVE SOUND ON BGM--
3780 FOR I=0 TO 11:SOUND 6,4:SOUND 7,&H98:SOUND 10,&H1F:SOUND 8,5:SOUND 9,5:NEXT
3790 SOUND 7,&HB8:SOUND 10,0
3800 IF PLAY(0) THEN 3800
3810 RETURN
3820 '--MOVE SOUND OFF BGM--
3830 IF(SM=1) THEN RETURN
3840 SOUND 6,4:SOUND 7,&HB7
3850 SOUND 8,&H10:SOUND 13,0
3860 SOUND 11,100:SOUND 12,2
3870 IF PLAY(0) THEN 3870
3880 RETURN
3890 '--WINDOW DATA INPUT--
3900 DIM NA$(6):DIM AR$(6):DIM JO$(6):DIM AC$(6):DIM HP%(6):DIM MH%(6)
3910 RESTORE 4010:FOR A=0 TO 5:READ NA$(A),AR$(A),JO$(A),AC$(A),HP%(A),MH%(A):NEXT A
3920 RETURN
3930 '--STATUS WINDOW DISPLAY--
3940 WX=0:WY=15
3950 LOCATE WX,WY+0:PRINT"())))))))))))))))))))))))))*"
3960 LOCATE WX,WY+1:PRINT"+Name     Class AC Hit Max ,"
3970 FOR A=0 TO 5:LOCATE WX,WY+2+A:PRINT USING "+&      & !_& & && ###_### ,";NA$(A),AR$(A),JO$(A),AC$(A),HP%(A),MH%(A):NEXT A
3980 LOCATE WX,WY+8:PRINT"-........................../";CHR$(&HB)'制御文字でカーソルをホームポジションへ
3990 RETURN
4000 '--STATUS WINDOW DATA--
4010 DATA ROCKCITY,E,SAMRAI,LO,123,134
4020 DATA 1re1,G,LORD,LO,140,152
4030 DATA TKZ80,G,LORD,9,80,102
4040 DATA Newt,N,NINJA,6,887,901
4050 DATA umipi,N,MAGE,2,451,501
4060 DATA にしかずひこ,G,BISHOP,0,702,721
4070 '--SPRITE DATA SET 矢印4方向、8ｘ4＝32バイト--
4080 RESTORE 4100:FOR I=0 TO31:READ D$:VPOKE&H3800+I,VAL("&H"+D$):NEXT:RETURN
4090 '--SPRITE DATA 4方向分8x4=32バイト--
4100 DATA 00,18,18,3C,3C,7E,00,00
4110 DATA 00,20,38,3E,3E,38,20,00
4120 DATA 00,00,7E,3C,3C,18,18,00
4130 DATA 00,04,1C,7C,7C,1C,04,00
4140 '--WALL COLOR READ 3色x5で15バイト VRAMに初期カラーを書き込み--
4150 RESTORE 4190:FOR I=0 TO 4:READ C1(I),C2(I),C3(I):NEXT I
4160 VP=&H2002:VPOKE VP,C1(C0):VPOKE VP+1,C2(C0):VPOKE VP+2,C3(C0)
4170 RETURN
4180 '--WALL COLOR--
4190 DATA &h61,&h91,&h81,&h41,&h71,&h51,&hc1,&h31,&h21
4200 DATA &ha1,&hf1,&hb1,&he1,&hb1,&hf1
4210 '--AUTO MAPPING INIT DATA SET--
4220 RESTORE 4680:RD%=0:WX=0:WY=0:WW=18:WH=18:SS=0:OP%=0
4230 GOSUB 3110
4240 LOCATE 0,22:PRINT "そのうちマシンごになおします"
4250 FOR Y=0 TO 15: FOR X=0 TO 15:OP=0
4260 READ R$:RD=VAL("&H"+R$):MZ(X,Y)=RD
4270 POKE &HC200+Y*16+X,RD
4280 C1=ABS((RD AND 3)=3)
4290 C2=ABS(((RD/4) AND 3)=3)
4300 C3=ABS(((RD/16) AND 3)=3)
4310 C4=ABS(((RD/64) AND 3)=3)
4320 OP=0:OP=(C4*8)OR(C3*4)OR(C2*2)OR C1
4330 VPOKE (&H2E02-Y*32+X+1),17
4340 MZ(X,Y)=MZ(X,Y) OR ((OP AND 15)*256)'9bit-12bit MAP PARTS
4350 IF((RD AND 255)=&HFF) THEN SS=SS+1'STONE NR
4360 LOCATE 0,20:PRINT "MAZE DATA SETTING";:PRINT INT(100*(Y*16+X)/(16*16-1));"percent"
4370 NEXT X,Y:
4380 LOCATE 0,22:PRINT "DATA LOAD COMPLETE"
4390 FOR I=0 TO 500 : NEXT I:LOCATE 0,20:PRINT SPC(32):LOCATE 0,22:PRINT SPC(32)
4400 RETURN
4410 '--POINT EVENT READ--
4420 RESTORE 4850:PT=0:PP=0
4430 READ PT:READ X:READ Y:PP=0
4440 IF PT=0 THEN RETURN 'END DATA
4450 MZ(X,Y)=MZ(X,Y)AND &HFFF
4460 PP=(PT AND 7)*(2^12):MZ(X,Y)=MZ(X,Y) OR PP
4470 GOTO 4430
4480 '--BGM DATA SET--
4490 PL=&HC4E0
4500 POKE PL,0:POKE PL+1,&HC5:POKE PL+2,&H5
4510 POKE PL+3,&HC5:POKE PL+4,&HA:POKE PL+5,&HC5
4520 RETURN 
4530 '--PRINT SET BACK SCREEN--
4540 POKE &HF922,0:POKE &HF923,&H2C:RETURN
4550 '--PRINT SET MAIN SCREEN--
4560 POKE &HF922,0:POKE &HF923,&H18:RETURN
4570 '--TITLE RETURN--
4580 GOSUB 4550:WX=7:WY=11:WW=14:WH=3:GOSUB 3110
4590 LOCATE WX+2,WY+1:PRINT"おつかれさまでした。"
4600 FOR I=0 TO 1000: NEXT I
4610 A=USR3(0):GOTO 1420
4620 '--ON STOP GOSUB--
4630 WX=7:WY=11:WW=14:WH=3:GOSUB 3110
4640 LOCATE WX+2,WY+1:PRINT"おつかれさまでした。"
4650 FOR I=0 TO 1000: NEXT I
4660 A=USR3(0):SCREEN 0:END 
4670 '------------ MAZE DATA --------------
4680 DATA F0,30,33,33,33,30,3C,FF,F0,3C,FF,FF,F0,32,33,3C
4690 DATA C0,0F,FF,FC,FC,C3,0C,FF,C3,0B,B0,3C,CC,EF,FB,8C
4700 DATA CC,FF,FC,CE,C3,3C,C3,3C,F0,38,83,0B,88,BF,FE,CC
4710 DATA CC,F3,03,2C,F8,83,3F,CC,C0,0B,BC,FF,C3,33,23,0F
4720 DATA C0,30,3C,CE,CB,B2,33,0F,CE,FB,83,3E,F0,30,33,3C
4730 DATA C0,03,0F,E3,33,33,30,33,22,33,33,2B,83,07,7F,CC
4740 DATA CC,FF,FF,F0,33,3C,CC,F0,2C,FB,B3,3F,F8,FE,F3,0C
4750 DATA CC,F0,3C,C8,BF,CB,8C,C3,0E,C3,3C,F3,0F,C3,3B,CC
4760 DATA C8,80,0C,C3,33,0F,CC,F8,A0,38,8F,E3,3C,F0,3F,8C
4770 DATA CC,C3,0F,FF,FF,F0,0C,CF,C3,0B,B3,3F,CF,8F,F3,0C
4780 DATA C0,33,33,33,33,00,00,33,33,33,33,3B,B0,34,7F,CC
4790 DATA CC,F0,3C,FF,FF,C3,0C,F0,38,B3,3F,FF,C3,03,33,0E
4800 DATA CE,C2,0F,F0,33,3C,CC,C2,0B,BC,FF,FF,D0,33,32,2C
4810 DATA E0,23,3C,C8,BF,C8,8C,F0,3C,CC,F0,3C,C8,BF,EF,CC
4820 DATA CC,FE,CC,C3,33,0F,CC,C2,0F,CE,C3,0E,CC,FE,FB,8C
4830 DATA C3,23,0F,F3,33,33,03,23,33,23,33,2F,C3,23,33,0F
4840 '------------ POINT EVENT (1 TELEPORTER, 0 END ) -------------
4850 DATA 1,4,1, 1,14,2, 1,1,3, 1,2,4, 1,11,6, 1,4,7, 1,14,9, 1,7,11,1,14,13
4860 DATA 2,6,1, 2,8,5,  2,15,9,2,6,10,2,14,12,2,9,15
4870 DATA 0,0,0
5000 '======== STAFF ROLL ========
5010 RESTORE 5110:WX=1:WY=0:WW=26:WH=23:GOSUB3110:EX=WX+1:EY=WY+1
5020 READ X,Y,M$
5030 IF X=255 THEN 5270
5040 I=LEN(M$)
5050 FOR J=1 TO I
5060 LOCATE EX+X,EY+Y
5070 PRINT MID$(M$,J,1)+CHR$(255)
5080 X=X+1:FOR K=0 TO 20:NEXT K
5090 NEXT J:LOCATE EX+X,EY+Y:PRINT" ":  
5100 GOTO 5020
5110 DATA 1,1,"MAIN PROGRAM:"
5120 DATA 7,3,msx2rockcity
5130 DATA 1,5,"SPECIAL THANKS:"
5140 DATA 7,7,1re1
5150 DATA 7,8,TKZ80
5160 DATA 7,9,Newt
5170 DATA 7,10,umipi
5180 DATA 7,11,kuran_kuran
5190 DATA 7,12,にしかずひこ
5200 DATA 2,14,_ここまでつきあってくれたかたがた、_
5210 DATA 2,15,_ありがとうございました。_
5220 DATA 2,16,_MSXはふめつです。これからも。_
5230 DATA 2,18,"())))))))))))))))))*"
5240 DATA 2,19,"+  MSX FOREVER!!!  ,"
5250 DATA 2,20,"-................../"
5260 DATA 255,,
5270 FOR I = 0 TO 1000
5280 C=I MOD 5:VP=&H2002:VPOKE VP,C1(C):VPOKE VP+1,C2(C):VPOKE VP+2,C3(C)
5290 NEXT I
5300 RETURN
5310 '======== GAMECLEAR MESSAGE ========
5320 WX=4:WY=4:WW=20:WH=17:GOSUB3110
5330 LOCATE WX+2,WY+2:PRINT "CONGRATULATIONS!!"
5340 LOCATE WX+2,WY+5:PRINT "CLEAR TIME:":LOCATE WX+9,WY+7:PRINT USING "###:##:##";TM,TS,TN
5350 LOCATE WX+2,WY+10:PRINT "RANK: ";
5360 IF(TM<15) THEN PRINT "SS":GOTO 5430
5370 IF(TM<20) THEN PRINT "S":GOTO 5430
5380 IF(TM<30) THEN PRINT "A":GOTO 5430
5390 IF(TM<40) THEN PRINT "B":GOTO 5430
5400 IF(TM<50) THEN PRINT "C":GOTO 5430
5410 IF(TM<60) THEN PRINT "D":GOTO 5430
5420 PRINT "E"
5430 LOCATE WX+2,WY+13:PRINT "こんなゲームをクリアしてくれて
5440 LOCATE WX+2,WY+14:PRINT "ありがとう"
5450 FOR I = 0 TO 600
5460 C=I MOD 5:VP=&H2002:VPOKE VP,C1(C):VPOKE VP+1,C2(C):VPOKE VP+2,C3(C)
5470 NEXT I
5480 RETURN
5490 '======== GAMEOVER MESSAGE ========
5500 WX=4:WY=4:WW=20:WH=18:GOSUB3110
5510 LOCATE WX+4,WY+2:PRINT "UNLUCKY!!!!!"
5520 LOCATE WX+2,WY+5:PRINT "PLAY TIME:":LOCATE WX+9,WY+7:PRINT USING "###:##:##";TM,TS,TN
5530 P=INT(100!*(WK/(256!-SS)))
5540 LOCATE WX+2,WY+10:PRINT "PERCENTAGE: ";P
5550 LOCATE WX+2,WY+12:PRINT "RANK: ";
5560 IF(P>90)THEN PRINT "A":GOTO 5610  
5570 IF(P>80)THEN PRINT "B":GOTO 5610
5580 IF(P>70)THEN PRINT "C":GOTO 5610
5590 IF(P>60)THEN PRINT "D":GOTO 5610
5600 PRINT "E"
5610 LOCATE WX+2,WY+14:PRINT "うんがなかったね"
5620 LOCATE WX+2,WY+15:PRINT "またあそんでね、ありがとう"
5630 FOR I = 0 TO 600
5640 C=I MOD 5:VP=&H2002:VPOKE VP,C1(C):VPOKE VP+1,C2(C):VPOKE VP+2,C3(C)
5650 NEXT I
5660 RETURN
5670 '======== GAME MEMORY CLEAR ========
5680 VDP(2)=6:GOSUB 4530:FOR Y=0TO 15:FOR X=0 TO15:MZ(X,Y)=MZ(X,Y)AND &H7FFF:VPOKE &H2E02-Y*32+X+1,17:NEXT X,Y 'FLAG CLEAR
5690 RETURN
